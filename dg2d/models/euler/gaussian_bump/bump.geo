Mesh.RecombineAll = 1;
Mesh.RecombinationAlgorithm = 1;

nx = 101;

xmin = -1.5;
xmax =  1.5;
ymin =  0.0;
ymax =  0.8;

hx = (xmax - xmin)/(nx - 1);
ny = (ymax - ymin)/hx + 1;

Macro bump
   y = 0.0625*Exp(-25.0*x*x);
Return

//Points on lower surface
For i In {1:nx}
   x = xmin + (i-1)*hx;
   Call bump;
   Point(i) = {x,y,0.0};
EndFor

//Points on Upper Surface
For i In {nx+1:2*nx}
   x = xmin + (i-(nx+1))*hx;
   y = ymax;
   Point(i) = {x,y,0.0};
EndFor

Line(1) = {1:nx};
Line(2) = {nx,2*nx};
Line(3) = {2*nx,nx+1};
Line(4) = {nx+1,1};

Line Loop(1) = {1,2,3,4};

Transfinite Line{1,-3} = nx;
Transfinite Line{2,-4} = ny;

Plane Surface(1) = {1};

Transfinite Surface{1};
//Recombine{1};

Physical Surface(100) = {1}; // domain
Physical Line(1) = {1};      // Bottom
Physical Line(2) = {2};      // Outlet
Physical Line(3) = {3};      // Top
Physical Line(4) = {4};      // Inlet
